buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public" }
    }
    dependencies {
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'release'

group = 'com.github.stormcat24'
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    optional
}

dependencies {
    compile(
        [group: 'redis.clients', name: 'jedis', version: '2.5.1'],
        [group: 'org.projectlombok', name: 'lombok', version: '1.12.6']
    )
    testCompile(
        [group: 'junit', name: 'junit', version: '4.11'],
        [group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0']
    )
}

task wrapper(type: Wrapper) { gradleVersion = "1.10" }

task sourcesJar(type: Jar, dependsOn:classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

sourceSets {
    main {
        java { srcDirs = ['src/main/java']}
    }

    test {
        groovy { srcDirs = ['src/test/groovy', 'src/test/java']}
    }
}

release {
    failOnPublishNeeded = false
}

// リリースタグの直後にnexusへJarをアップロード
createReleaseTag << {
    uploadArchives.execute()
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:${System.getProperty('user.home')}/.m2/repository")
        }
    }
}
